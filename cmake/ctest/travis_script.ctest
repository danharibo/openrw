cmake_minimum_required(VERSION 3.2.2)

set(TRAVIS "$ENV{TRAVIS}")

if(TRAVIS)
    message("travis_script: Travis detected")
    set(CTEST_SITE_BASE "Travis")
    string(SUBSTRING "$ENV{TRAVIS_COMMIT}" 0 8 SHORT_COMMIT)
    set(CTEST_BUILD_NAME "$ENV{TRAVIS_EVENT_TYPE}-$ENV{TRAVIS_REPO_SLUG}-$ENV{TRAVIS_BRANCH}-${SHORT_COMMIT}")
else()
    message("travis_script: Travis NOT detected")
endif()

get_filename_component(SRC_DIR "${SRC_DIR}" ABSOLUTE)
get_filename_component(BIN_DIR "${BIN_DIR}" ABSOLUTE)

set(CTEST_SOURCE_DIRECTORY "${SRC_DIR}")
set(CTEST_BINARY_DIRECTORY "${BIN_DIR}")
set(CTEST_CMAKE_GENERATOR "Unix Makefiles")

message(STATUS "Removing binary directory '${CTEST_BINARY_DIRECTORY}'...")
if(EXISTS "${BIN_DIR}/CMakeCache.txt")
    message(STATUS "CMakeCache.txt file exists ==> removing")
    ctest_empty_binary_directory("${BIN_DIR}")
endif()

set(CTEST_SITE "${CTEST_SITE_BASE}-NODATA")
ctest_start("Continuous")

ctest_configure(
    OPTIONS
        "-DBUILD_VIEWER=1;-DBUILD_TESTS=1;-DTESTS_NODATA=1"
    RETURN_VALUE
        CONFIGURE_RESULT_NODATA
    )
ctest_build(
    NUMBER_ERRORS BUILD_NB_ERRORS_NODATA
    NUMBER_WARNINGS BUILD_NB_WARNINGS_NODATA
    )
ctest_test(
    RETURN_VALUE TEST_RESULT
    )

if(TRAVIS)
    ctest_submit(
        RETURN_VALUE SUBMIT_RESULT
        )

    if(NOT SUBMIT_RESULT EQUAL "0")
        message(WARNING "Submit failure detected!")
    endif()
endif()

set(CTEST_SITE "${CTEST_SITE_BASE}-DATA")
ctest_start("Continuous")

ctest_configure(
    OPTIONS
        "-DBUILD_VIEWER=1;-DBUILD_TESTS=1;-DTESTS_NODATA=0"
    RETURN_VALUE
        CONFIGURE_RESULT_DATA
    )
ctest_build(
    NUMBER_ERRORS BUILD_NB_ERRORS_DATA
    NUMBER_WARNINGS BUILD_NB_WARNINGS_DATA
    )

if(TRAVIS)
    ctest_submit(
        RETURN_VALUE SUBMIT_RESULT
        )

    if(SUBMIT_RESULT)
        message(FATAL_ERROR "Submit failure detected!")
    endif()
endif()

if(CONFIGURE_RESULT_DATA OR CONFIGURE_RESULT_NODATA)
    message(FATAL_ERROR "Configure error detected!")
endif()

if(BUILD_NB_ERRORS_DATA OR BUILD_NB_ERRORS_NODATA)
    message(FATAL_ERROR "Build failure detected!")
endif()

if(TEST_RESULT)
    message(FATAL_ERROR "Test failure detected!")
endif()
